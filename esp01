#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <ESP8266HTTPClient.h>
#include <WiFiClient.h>

ESP8266WiFiMulti WiFiMulti;

const char* ssid = "mesh_hotspot";
const char* password = "passWord";
const char* serverURL = "http://clouai.in/mesh/log.php";  // Base URL of the server

void setup() {
  Serial.begin(115200);
  Serial.println("\n\n[SETUP] Starting...");

  WiFi.mode(WIFI_STA);
  WiFiMulti.addAP(ssid, password);

  for (uint8_t t = 4; t > 0; t--) {
    Serial.printf("[SETUP] WAIT %d...\n", t);
    Serial.flush();
    delay(1000);
  }

  Serial.println("[READY] Waiting for serial input...");
}

void loop() {
  // Check if serial data is available
  if (Serial.available()) {
    String receivedData = Serial.readStringUntil('|');  // Read until '|' is received
    receivedData.trim();  // Remove extra spaces/newlines

    if (receivedData.length() > 0) {
      Serial.println("[INFO] Received: " + receivedData);
      sendDataToServer(receivedData);  // Send the received data
    }
  }
}

//_________________________________________ Send Data to Server
void sendDataToServer(String data) {
  if (WiFiMulti.run() != WL_CONNECTED) {
    Serial.println("[ERROR] WiFi not connected!");
    return;
  }

  WiFiClient client;
  HTTPClient http;

  String fullURL = String(serverURL) + "?" + data;  // Append received data to URL

  Serial.print("[HTTP] Sending data to: ");
  Serial.println(fullURL);

  if (http.begin(client, fullURL)) {  // Start HTTP request
    int httpCode = http.GET();  // Send GET request

    if (httpCode > 0) {
      Serial.printf("[HTTP] GET... Code: %d\n", httpCode);

      if (httpCode == HTTP_CODE_OK || httpCode == HTTP_CODE_MOVED_PERMANENTLY) {
        String payload = http.getString();
        Serial.println("Server Response: " + payload);
      }
    } else {
      Serial.printf("[HTTP] GET... failed, error: %s\n", http.errorToString(httpCode).c_str());
    }

    http.end();
  } else {
    Serial.println("[HTTP] Unable to connect");
  }
}
